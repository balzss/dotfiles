function prompt_balzss_precmd {
  # Check for untracked files or updated submodules since vcs_info does not.
  if [[ -n $(git ls-files --other --exclude-standard 2> /dev/null) ]]; then
    branch_format="${_prompt_balzss_colors[4]}[%b ✘]%f%u%c"
  else
    branch_format="${_prompt_balzss_colors[4]}[%b ✔]%f%u%c"
  fi

  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"

  vcs_info 'prompt'

  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_balzss_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook
  autoload -Uz vcs_info

  # Add hook for calling vcs_info before each command.
  add-zsh-hook precmd prompt_balzss_precmd

  # Use extended color pallete if available.
  if [[ $TERM = *256color* || $TERM = *rxvt* ]]; then
    _prompt_balzss_colors=(
      "%F{72}"  # Turquoise
      "%F{166}" # Orange
      "%F{135}" # Purple
      "%F{175}" # Hotpink
      "%F{109}" # Limegreen
      "%F{245}" # Limegreen
    )
  else
    _prompt_balzss_colors=(
      "%F{cyan}"
      "%F{yellow}"
      "%F{magenta}"
      "%F{red}"
      "%F{green}"
    )
  fi

  # Formats:
  #   %b - branchname
  #   %u - unstagedstr (see below)
  #   %c - stagedstr (see below)
  #   %a - action (e.g. rebase-i)
  #   %R - repository path
  #   %S - path in the repository
  local branch_format="[${_prompt_balzss_colors[4]}%b%f%u%c]"
  local action_format="(${_prompt_balzss_colors[4]}%a%f)"
  local unstaged_format="${_prompt_balzss_colors[4]}%f"
  local staged_format="${_prompt_balzss_colors[4]}%f"

  # Set vcs_info parameters.
  zstyle ':vcs_info:*' enable bzr git hg svn
  zstyle ':vcs_info:*:prompt:*' check-for-changes true
  zstyle ':vcs_info:*:prompt:*' unstagedstr "${unstaged_format}"
  zstyle ':vcs_info:*:prompt:*' stagedstr "${staged_format}"
  zstyle ':vcs_info:*:prompt:*' actionformats "${branch_format}${action_format}"
  zstyle ':vcs_info:*:prompt:*' formats "${branch_format}"
  zstyle ':vcs_info:*:prompt:*' nvcsformats   ""

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format '(%v)'

  # Define prompts.
  PROMPT="
${_prompt_balzss_colors[5]}[%d]%f "'${vcs_info_msg_0_}'"
"'$python_info[virtualenv]'" ${_prompt_balzss_colors[6]}→%f "
  RPROMPT=''
}
prompt_balzss_setup "$@"
